project.version="2.0.12"

task copyApplication(type: Sync) {
    def installDistTask = project(':com.minres.coredsl.ide').tasks.installDist
    dependsOn installDistTask
    from installDistTask.outputs
    into 'src/coredsl'
}

clean {
    doLast {
        delete copyApplication.outputs
    }
}

npmInstall.dependsOn copyApplication

task installExtension(type: Exec, dependsOn: vscodeExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args '--install-extension', vscodeExtension.destPath
}

task startCode(type:Exec, dependsOn: installExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args "$rootProject.projectDir/demo/", '--new-window'
}

task publish(dependsOn: vscodeExtension, type: NodeTask) {
    script = file("$rootProject.projectDir/node_modules/vsce/out/vsce")
    args = [ 'publish', '-p', System.getenv('ACCESS_TOKEN'), "--packagePath", "${project.buildDir}/coredsl-vscode-${project.version}.vsix"]
    execOverrides {
        workingDir = projectDir
    }
}