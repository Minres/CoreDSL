grammar com.minres.coredsl.CoreDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate coreDsl "http://www.minres.com/coredsl/CoreDsl/2.0"

///////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Top Level Definitions ////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////

DescriptionContent: (imports+=Import)* definitions+=(InstructionSet | CoreDef)*;
Import: 'import' importURI=STRING;

InstructionSet: 'InstructionSet' name=ID ( 'extends' superType=[InstructionSet] )? '{' ISA '}';
CoreDef: 'Core' name=ID ( 'provides' providedInstructionSets+=[InstructionSet] (',' providedInstructionSets+=[InstructionSet])*)? '{' ISA '}';

fragment ISA:
	('architectural_state' '{' (archStateBody+=DeclarationStatement | archStateBody+=ExpressionStatement | typeDeclarations+=UserTypeDeclaration)+ '}')? &
	('functions' '{' functions+=FunctionDefinition+ '}')? &
	('instructions' commonInstructionAttributes+=Attribute* '{' instructions+=Instruction+ '}')? &
	('always' commonAlwaysBlockAttributes+=Attribute* '{' alwaysBlocks+=AlwaysBlock+ '}')?;

FunctionDefinition:
    extern?='extern'
    returnType=TypeSpecifier name=ID
    '(' (parameters+=ParameterDeclaration (',' parameters+=ParameterDeclaration)*)? ')'
    attributes+=Attribute* t_semicolon=';'
    |
    returnType=TypeSpecifier name=ID
    '(' (parameters+=ParameterDeclaration (',' parameters+=ParameterDeclaration)*)? ')'
    attributes+=Attribute* body=CompoundStatement;

Instruction:
	name=ID attributes+=Attribute* '{' 
		('encoding' ':' encoding=Encoding ';')
		('assembly' ':' (assembly=STRING | '{' mnemonic=STRING ',' assembly=STRING '}') ';')?
		('behavior' ':' behavior=Statement)
	'}';

AlwaysBlock:
	name=ID attributes+=Attribute* behavior=CompoundStatement;

Encoding: fields+=EncodingField ('::' fields+=EncodingField)*;
EncodingField: BitValue | BitField;
BitValue: value=INTEGER;
BitField: name=ID LEFT_BR startIndex=IntegerConstant ':' endIndex=IntegerConstant RIGHT_BR;

////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Statements ////////////////////////////////
////////////////////////////////////////////////////////////////////////////

Statement:
	EmptyStatement
	| SpawnStatement
	| CompoundStatement
	| ExpressionStatement
	| DeclarationStatement
	| IfStatement
	| SwitchStatement
	| ContinueStatement
	| BreakStatement
	| ReturnStatement
	| LoopStatement;

EmptyStatement: {EmptyStatement} ';';
SpawnStatement: t_spawn='spawn' body=Statement;
CompoundStatement: {CompoundStatement} t_openingBrace='{' statements+=Statement* t_closingBrace='}';
ExpressionStatement: expression=AssignmentExpression ';';
DeclarationStatement: declaration=MultiInitDeclaration ';';

//////////////////////////////// Flow Control ////////////////////////////////

IfStatement:
	'if' '(' condition=Expression ')' thenBody=Statement (=> 'else' elseBody=Statement)?;

SwitchStatement:
	'switch' '(' condition=Expression ')' '{' sections+=SwitchSection* t_end='}';

SwitchSection:
	{CaseSection} t_case='case' condition=Expression ':' body+=Statement* |
	{DefaultSection} t_default='default' ':' body+=Statement*;

ContinueStatement: {ContinueStatement} 'continue' ';';
BreakStatement: {BreakStatement} 'break' ';';
ReturnStatement: {ReturnStatement} t_return='return' value=ConditionalExpression? ';';

//////////////////////////////// Loops ////////////////////////////////

LoopStatement: WhileLoop | ForLoop | DoLoop;

WhileLoop:
	'while' '(' condition=Expression ')' body=Statement;

ForLoop:
	'for' '('
	(startDeclaration=MultiInitDeclaration | startExpression=AssignmentExpression?) ';'
	(condition=Expression?) ';'
	(loopExpressions+=AssignmentExpression (',' loopExpressions+=AssignmentExpression)*)?
	')' body=Statement;

DoLoop:
	'do' body=Statement 'while' '(' condition=Expression ')' ';';

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Declarations ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////
	
ParameterDeclaration returns Declaration: Declaration<false, false>;
MultiInitDeclaration returns Declaration: Declaration<true, true>;
MultiDeclaration returns Declaration: Declaration<true, false>;

Declaration<allowMultiple, allowInit>:
	(storage+=StorageClassSpecifier | qualifiers+=TypeQualifier)*
	type=TypeSpecifier declarators+=Declarator<allowInit> (<allowMultiple> ',' declarators+=Declarator<allowInit>)*;

Declarator<allowInit>:
	alias?='&'?
	name=ID
	(LEFT_BR dimensions+=Expression RIGHT_BR)*
	attributes+=Attribute*
	(<allowInit> t_equals='=' initializer=Initializer)?;

NamedEntity: FunctionDefinition | Declarator<true> | BitField;
    
Initializer: ExpressionInitializer | ListInitializer;
ExpressionInitializer: value=Expression;
ListInitializer: '{' initializers+=Initializer (',' initializers+=Initializer)* ','? '}';

Attribute:
	DoubleLeftBracket attributeName=ID
	('=' parameters+=Expression | '(' parameters+=Expression (',' parameters+=Expression)* ')')?
	DoubleRightBracket;

//////////////////////////////// Type Declarations ////////////////////////////////

UserTypeDeclaration: CompositeTypeDeclaration | EnumTypeDeclaration;
CompositeTypeDeclaration : StructTypeDeclaration | UnionTypeDeclaration;
StructTypeDeclaration: 'struct' name=ID '{' (members+=MultiDeclaration ';')* '}';
UnionTypeDeclaration: 'union' name=ID '{' (members+=MultiDeclaration ';')* '}';
EnumTypeDeclaration: 'enum' name=ID '{' members+=Declarator<true> (',' members+=Declarator<true>)* ','? '}';

//////////////////////////////// Type Specifiers ////////////////////////////////

TypeSpecifier: VoidTypeSpecifier | PrimitiveTypeSpecifier | UserTypeSpecifier;

PrimitiveTypeSpecifier: IntegerTypeSpecifier | BoolTypeSpecifier;

IntegerTypeSpecifier: shorthand=IntegerSizeShorthand | signedness=IntegerSignedness (shorthand=IntegerSizeShorthand | '<' size=PrimaryExpression '>');
BoolTypeSpecifier: {BoolTypeSpecifier} 'bool';
VoidTypeSpecifier: {VoidTypeSpecifier} 'void';

UserTypeSpecifier: StructTypeSpecifier | UnionTypeSpecifier | EnumTypeSpecifier;
	
StructTypeSpecifier: 'struct' target=[StructTypeDeclaration];
UnionTypeSpecifier: 'union' target=[UnionTypeDeclaration];
EnumTypeSpecifier: 'enum' target=[EnumTypeDeclaration];

/////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Expressions ////////////////////////////////
/////////////////////////////////////////////////////////////////////////////

Expression: AssignmentExpression;

AssignmentExpression returns Expression:
	ConditionalExpression
	({AssignmentExpression.target=current}
		operator=('=' | '*=' | '/=' | '%=' | '+=' | '-=' | '<<=' | '>>=' | '&=' | '^=' | '|=')
		value=AssignmentExpression
	)?;

ConditionalExpression returns Expression:
	ConcatenationExpression
	({ConditionalExpression.condition=current}
		t_question='?' thenExpression=AssignmentExpression
		t_colon=':' elseExpression=ConditionalExpression
	)?;

ConcatenationExpression returns Expression:
	LogicalOrExpression
	({ConcatenationExpression.parts+=current}
		('::' parts+=LogicalOrExpression)+
	)?;

LogicalOrExpression returns Expression: LogicalAndExpression ({InfixExpression.left=current} operator='||' right=LogicalAndExpression)*;
LogicalAndExpression returns Expression: InclusiveOrExpression ({InfixExpression.left=current} operator='&&' right=InclusiveOrExpression)*;
InclusiveOrExpression returns Expression: ExclusiveOrExpression ({InfixExpression.left=current} operator='|' right=ExclusiveOrExpression)*;
ExclusiveOrExpression returns Expression: AndExpression ({InfixExpression.left=current} operator='^' right=AndExpression)*;
AndExpression returns Expression: EqualityExpression ({InfixExpression.left=current} operator='&' right=EqualityExpression)*;
EqualityExpression returns Expression: RelationalExpression ({InfixExpression.left=current} operator=('=='|'!=') right=RelationalExpression)*;
RelationalExpression returns Expression: ShiftExpression ({InfixExpression.left=current} operator=('<'|'>'|'<='|'>=') right=ShiftExpression)*;
ShiftExpression returns Expression: AdditiveExpression ({InfixExpression.left=current} operator=('<<'|'>>') right=AdditiveExpression)*;
AdditiveExpression returns Expression: MultiplicativeExpression ({InfixExpression.left=current} operator=('+'|'-') right=MultiplicativeExpression)*;
MultiplicativeExpression returns Expression: CastExpression ({InfixExpression.left=current} operator=('*' | '/' | '%') right=CastExpression)*;

CastExpression returns Expression:
	PrefixExpression
	| {CastExpression} '(' targetType=TypeSpecifier ')' operand=CastExpression
	| {CastExpression} '(' signedness=IntegerSignedness ')' operand=CastExpression;

PrefixExpression returns Expression:
	PostfixExpression
	| {PrefixExpression} operator=('++' | '--') operand=PrefixExpression
	| {PrefixExpression} operator=('+' | '-' | '~' | '!') operand=PrefixExpression
    ;

PostfixExpression returns Expression:
	PrimaryExpression
	( {IndexAccessExpression.target=current} LEFT_BR index=Expression (t_colon=':' endIndex=Expression)? RIGHT_BR
	| {MemberAccessExpression.target=current} operator='.' declarator=[Declarator]
	| {PostfixExpression.operand=current} operator='++'
	| {PostfixExpression.operand=current} operator='--'
	)*;

PrimaryExpression: ParenthesisExpression | EntityReference | FunctionCallExpression | IntrinsicExpression | Constant;

ParenthesisExpression: '(' inner=Expression ')';

EntityReference: target=[NamedEntity];

FunctionCallExpression:
	target=EntityReference '(' (arguments+=Expression (',' arguments+=Expression)*)? ')';
    
IntrinsicExpression:
	function=('sizeof' | 'bitsizeof' | 'offsetof' | 'bitoffsetof' | '__encoding_size')
	('(' (arguments+=(Expression|TypeSpecifier) (',' arguments+=(Expression|TypeSpecifier))*)? ')')?;

//////////////////////////////// Constants ////////////////////////////////

Constant: IntegerConstant | BoolConstant | StringConstant;

IntegerConstant hidden(WS): value=INTEGER;
BoolConstant: value=BOOLEAN;
StringConstant: value=STRING;

//////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Enumerations ////////////////////////////////
//////////////////////////////////////////////////////////////////////////////

enum IntegerSignedness : SIGNED='signed' | UNSIGNED='unsigned';
enum IntegerSizeShorthand : INT='int' | CHAR='char' | SHORT='short' | LONG='long';

enum TypeQualifier: CONST='const' | VOLATILE='volatile';
enum StorageClassSpecifier: EXTERN='extern' | REGISTER='register';

////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// Terminal Rules ////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// The following 2 rules are needed so that XText does not generate the terminal symbols
// '[[' and ']]', which are always eaten by the lexer so that a[b[3]] is not recognized.
DoubleLeftBracket hidden(WS, ML_COMMENT, SL_COMMENT): LEFT_BR LEFT_BR;
DoubleRightBracket hidden(WS, ML_COMMENT, SL_COMMENT): RIGHT_BR RIGHT_BR;

terminal LEFT_BR: '[';
terminal RIGHT_BR: ']';

terminal BOOLEAN returns ecore::EBoolean: 'true' | 'false';

terminal INTEGER returns ecore::EBigInteger: (BINARYINT | HEXADECIMALINT | OCTALINT | DECIMALINT | VLOGINT);

terminal fragment BINARYINT: ('0b' | '0B') BIN_DIGIT+ ('_' BIN_DIGIT+)*;
terminal fragment OCTALINT: '0' ('_')? OCT_DIGIT+ ('_' OCT_DIGIT+)*;
terminal fragment DECIMALINT: '0' | ('1'..'9') DEC_DIGIT* ('_' DEC_DIGIT+)*;
terminal fragment HEXADECIMALINT: ('0x'|'0X') HEX_DIGIT+ ('_' HEX_DIGIT+)*;
terminal fragment VLOGINT: DEC_DIGIT+ "'" 's'? ('b' BIN_DIGIT+ | 'o' OCT_DIGIT+ | 'd' DEC_DIGIT+ | 'h' HEX_DIGIT+); 

terminal fragment BIN_DIGIT: '0'..'1';
terminal fragment OCT_DIGIT: '0'..'7';
terminal fragment DEC_DIGIT: '0'..'9';
terminal fragment HEX_DIGIT: DEC_DIGIT | 'a'..'f' | 'A'..'F';

@Override 
terminal INT returns ecore::EInt: '~this one has been deactivated';
    
@Override 
terminal ID : ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;